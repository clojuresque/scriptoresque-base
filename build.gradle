buildscript {
    repositories {
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    dependencies {
        classpath "de.kotka.gradle:gradle-plugin:1.1.0"
    }
}

subprojects {
    ext.kotka = [
        repository:     "clojuresque/cljs-base",
        wrapperVersion: "1.8"
    ]

    apply plugin: "kotka"

    group = 'clojuresque'
    version = '0.1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }
}

project(':scriptoresque-base') {
    apply plugin: 'groovy'

    dependencies {
        compile gradleApi()
        compile localGroovy()

        compile 'clojuresque:clojuresque-common:1.0.0-SNAPSHOT'
        compile "de.kotka.gradle:gradle-utils:0.1.1"

        testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
    }

    compileGroovy {
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6"
    }

    processResources {
        inputs.property "version", {-> project.version}
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':scriptoresque-base-runtime') {
    apply plugin: 'java'

    dependencies {
        compile 'clojuresque:clojuresque-common-runtime:1.0.0-SNAPSHOT'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.8"
}
